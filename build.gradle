plugins {
   id 'groovy'
   id 'java-gradle-plugin'
   id "com.gradle.plugin-publish" version "0.15.0"
   id "com.github.breadmoirai.github-release" version "2.2.12"
   id 'org.unbroken-dome.test-sets' version '4.0.0'
   id "com.avast.gradle.docker-compose" version "0.14.3"
   id "com.github.ben-manes.versions" version "0.39.0"
   id "com.redpillanalytics.gradle-analytics" version "1.4.0"
   id 'com.adarshr.test-logger' version '3.0.0'
   id 'build-dashboard'
}

// send analytics
analytics {
   organization = 'Red Pill Analytics'
   bq {
      devops {
         bucket = 'rpa-gradle-analytics'
         dataset = 'gradle_analytics'
      }
   }
}

githubRelease {
   token findProperty('githubToken').toString()
   owner 'RedPillAnalytics'
   repo rootProject.name
   overwrite true
   releaseAssets jar.destinationDirectory.files()
}

dependencies {

   implementation gradleApi()
   implementation localGroovy()

   implementation 'org.slf4j:slf4j-simple:1.7.30'
   implementation 'com.konghq:unirest-java:3.11.10'
   implementation 'com.google.code.gson:gson:2.8.7'

   // Gradle Properties
   // It also has the Common library in it, thus the dependency here
   implementation 'gradle.plugin.com.redpillanalytics:gradle-properties:0.1.11'

   // testing
   testImplementation 'org.spockframework:spock-core:2.0-groovy-3.0'
}

// Default artifact naming.
group = 'com.redpillanalytics'

java {
   toolchain {
      languageVersion = JavaLanguageVersion.of(8)
   }
}

repositories {
   mavenCentral()
   maven {
      url "https://plugins.gradle.org/m2/"
   }
}

gradlePlugin {
   plugins {
      gradleConfluent {
         id = 'com.redpillanalytics.gradle-confluent'
         implementationClass = 'com.redpillanalytics.gradle.ConfluentPlugin'
      }
   }
}

pluginBundle {

   website = 'https://github.com/RedPillAnalytics/gradle-confluent'
   vcsUrl = 'https://github.com/RedPillAnalytics/gradle-confluent'

   plugins {
      gradleConfluent {
         id = 'com.redpillanalytics.gradle-confluent'
         displayName = 'gradle-confluent'
         description = "A plugin for deploying streaming applications to a Confluent Kafka cluster."
         tags = ['kafka', 'confluent', 'ksql', 'streams']
      }
   }
}

testSets {
   ksqlServerTest
   ksqlPipelinesTest
   deployTest
   buildTest
}

tasks.deployTest.mustRunAfter tasks.buildTest

task cleanJunit(type: Delete) {
   delete getTestResultsDir()
}

task cleanLibs(type: Delete) {
   delete libsDirectory.files()
}

task runAllTests {
   description 'Run all defined tests.'
   group 'verification'
}

tasks.withType(Test) {
   runAllTests.dependsOn it
   failFast true
   testLogging.showStandardStreams true
   systemProperty 'projectDir', temporaryDir
   systemProperty 'pipelineEndpoint', pipelineEndpoint
   systemProperty 'kafkaServers', kafkaServers
   systemProperty 'analyticsVersion', analyticsVersion
   useJUnitPlatform()
}

runAllTests.mustRunAfter tasks.composeUp

// create a publish task
task publish {
   description 'Custom publish task.'
   group 'publishing'
   dependsOn tasks.publishPlugins, tasks.githubRelease, tasks.build
}

tasks.githubRelease.mustRunAfter tasks.publishPlugins, tasks.build
tasks.deployTest.mustRunAfter tasks.buildTest
