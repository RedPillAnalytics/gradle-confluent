plugins {
   id 'groovy'
   id 'java-gradle-plugin'
   id "com.gradle.plugin-publish" version "1.1.0"
   id "com.github.breadmoirai.github-release" version "2.4.1"
   id 'org.unbroken-dome.test-sets' version '4.0.0'
   id "com.avast.gradle.docker-compose" version "0.16.11"
   id "com.github.ben-manes.versions" version "0.44.0"
   id "com.redpillanalytics.gradle-analytics" version "1.4.6"
   id 'com.adarshr.test-logger' version '3.2.0'
   id 'build-dashboard'
}

// send analytics
analytics {
   organization = 'Red Pill Analytics'
   bq {
      devops {
         bucket = 'rpa-gradle-analytics'
         dataset = 'gradle_analytics'
      }
   }
}

githubRelease {
   token project.findProperty('githubToken').toString()
   owner 'RedPillAnalytics'
   repo rootProject.name
   overwrite true
   targetCommitish "master"
   releaseAssets jar.destinationDirectory.files()
   generateReleaseNotes true
}

dependencies {
   implementation 'org.codehaus.groovy:groovy:3.0.13'
   implementation 'com.konghq:unirest-java:3.13.13'
   implementation 'com.google.code.gson:gson:2.10'

   // Gradle Properties
   // It also has the Common library in it, thus the dependency here
   implementation 'gradle.plugin.com.redpillanalytics:gradle-properties:1.0.7'

   // testing
   testImplementation 'org.spockframework:spock-core:2.1-groovy-3.0'
   testImplementation "org.testcontainers:spock:1.17.6"
   testImplementation "org.testcontainers:kafka:1.17.6"
}

// Default artifact naming.
group = 'com.redpillanalytics'

java {
   toolchain {
      languageVersion = JavaLanguageVersion.of(11)
   }
}

repositories {
   mavenCentral()
   maven {
      url "https://plugins.gradle.org/m2/"
   }
}

gradlePlugin {
   plugins {
      gradleConfluent {
         id = 'com.redpillanalytics.gradle-confluent'
         implementationClass = 'com.redpillanalytics.gradle.ConfluentPlugin'
         displayName = 'gradle-confluent'
      }
   }
}

pluginBundle {
   website = 'https://github.com/RedPillAnalytics/gradle-confluent'
   vcsUrl = 'https://github.com/RedPillAnalytics/gradle-confluent'
   description = "A plugin for deploying streaming applications to a Confluent Kafka cluster."
   tags = ['kafka', 'confluent', 'ksql', 'streams']
}

testSets {
   ksqlServerTest
   ksqlPipelinesTest
   deployTest
   buildTest
}

tasks.deployTest.mustRunAfter tasks.buildTest

task cleanJunit(type: Delete) {
   delete getTestResultsDir()
}

task cleanLibs(type: Delete) {
   delete libsDirectory.files()
}

task runAllTests {
   description 'Run all defined tests.'
   group 'verification'
}

tasks.withType(Test) {
   runAllTests.dependsOn it
   failFast true
   testLogging.showStandardStreams true
   systemProperty 'projectDir', temporaryDir
   systemProperty 'analyticsVersion', analyticsVersion
   useJUnitPlatform()
   //maxParallelForks = Runtime.runtime.availableProcessors()
}

tasks.publish.dependsOn tasks.publishPlugins, tasks.githubRelease, tasks.build

tasks.githubRelease.mustRunAfter tasks.publishPlugins, tasks.build
tasks.deployTest.mustRunAfter tasks.buildTest
