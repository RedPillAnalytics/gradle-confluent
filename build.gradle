plugins {
   id 'groovy'
   id 'java-gradle-plugin'
   id "com.gradle.plugin-publish" version "0.10.1"
   id "pl.allegro.tech.build.axion-release" version "1.10.2"
   id "com.github.breadmoirai.github-release" version "2.2.10"
   id 'org.unbroken-dome.test-sets' version '2.2.1'
   id "com.avast.gradle.docker-compose" version "0.10.7"
   id "com.brightsparklabs.gradle.baseline" version "0.1.0"
   id "com.redpillanalytics.gradle-analytics" version "1.2.4"
   id 'com.adarshr.test-logger' version '2.0.0'
   id 'build-dashboard'
}

// send analytics
analytics {
   organization = 'Red Pill Analytics'
   sinks {
      gs {
         prefix = 'rpa-gradle-analytics'
      }
   }
}

scmVersion {
   tag {
      prefix = 'v'
      versionSeparator = ''
   }
   ignoreUncommittedChanges = true
}

//set Gradle version to SCM Version
allprojects {
   project.version = scmVersion.version
}

githubRelease {
   token = githubToken
   owner = 'RedPillAnalytics'
   repo = 'gradle-confluent'
   tagName = "v${version}"
   releaseAssets = fileTree(dir: buildDir, includes: ["**/*${version}*", "**/*${version}*.jar"]).toList()
}

dependencies {

   compile gradleApi()
   compile localGroovy()

   compile group: 'org.slf4j', name: 'slf4j-simple', version: '+'
   compile 'com.konghq:unirest-java:+'

   // Gradle Properties
   // It also has the Common library in it, thus the dependency here
   compile "gradle.plugin.com.redpillanalytics:gradle-properties:+"

   // tsting
   testCompile 'org.spockframework:spock-core:1.3-RC1-groovy-2.5'
}

// Default artifact naming.
group = 'com.redpillanalytics'

repositories {
   jcenter()
   maven {
      url "https://plugins.gradle.org/m2/"
   }
}

gradlePlugin {
   plugins {
      gradleConfluent {
         id = 'com.redpillanalytics.gradle-confluent'
         implementationClass = 'com.redpillanalytics.gradle.ConfluentPlugin'
      }
   }
}

pluginBundle {

   website = 'https://github.com/RedPillAnalytics/gradle-confluent'
   vcsUrl = 'https://github.com/RedPillAnalytics/gradle-confluent'

   plugins {
      gradleConfluent {
         id = 'com.redpillanalytics.gradle-confluent'
         displayName = 'gradle-confluent'
         description = "A plugin for deploying streaming applications to a Confluent Kafka cluster."
         tags = ['kafka', 'confluent', 'ksql', 'streams']
         version = project.version
      }
   }
}

testSets {
   ksqlServerTest
   ksqlPipelinesTest
   deployTest
   buildTest
}

tasks.deployTest.mustRunAfter tasks.buildTest

task cleanJunit(type: Delete) {
   delete getTestResultsDir()
}

task cleanLibs(type: Delete) {
   delete getLibsDir()
}

def reportUrl = "${actionsBucket}/${rootProject.name}/${executionId}"
task publishTestReports(type: Exec) {
   description = 'Copy Gradle test reports to S3.'
   group = 'verification'
   executable 'aws'
   args 's3', 'sync', reportsDir, "s3://${reportUrl}"
   doLast {
      logger.warn "\nTest reports: https://s3.amazonaws.com/${reportUrl}/buildDashboard/index.html"
   }
   dependsOn buildDashboard
}

def versionUrl = "${docBucket}/${rootProject.name}/${version}"
task publishVersionDocs(type: Exec) {
   description = 'Publish version Groovydocs to S3.'
   group = 'publishing'
   executable 'aws'
   args 's3', 'sync', groovydoc.destinationDir.getPath(), "s3://${versionUrl}"
   doLast {
      logger.warn "\nVersion docs: https://s3.amazonaws.com/${versionUrl}/index.html"
   }
   dependsOn groovydoc
}

def latestUrl = "${docBucket}/${rootProject.name}/latest"
task publishLatestDocs(type: Exec) {
   description = 'Publish latest Groovydocs to S3.'
   group = 'publishing'
   executable 'aws'
   args 's3', 'sync', groovydoc.destinationDir.getPath(), "s3://${latestUrl}"
   doLast {
      logger.warn "\nLatest docs: https://s3.amazonaws.com/${latestUrl}/index.html"
   }
   dependsOn groovydoc
}

task runAllTests {
   description 'Run all defined tests.'
   group 'verification'
}

tasks.withType(Test) {
   runAllTests.dependsOn it
   publishTestReports.mustRunAfter it
   failFast true
   ignoreFailures true
   testLogging.showStandardStreams true
   systemProperty 'projectDir', temporaryDir
   systemProperty 'pipelineEndpoint', pipelineEndpoint
   systemProperty 'kafkaServers', kafkaServers
}

//dockerCompose.isRequiredBy tasks.runAllTests

// create a publish task
task publish {
   description 'Custom publish task.'
   group 'publishing'
   dependsOn tasks.publishPlugins, tasks.publishVersionDocs, tasks.publishLatestDocs, tasks.githubRelease
}

tasks.githubRelease.mustRunAfter tasks.publishPlugins
